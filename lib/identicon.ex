defmodule Identicon do
  @moduledoc """
    Provides methods for creating and coloring an Identicon
  """
  @doc """
    runs all functions in module and creates and saves and Identicon
  """

  def main(input) do
    input
    |> hash_input
    |> pick_color
    |> build_grid
    |> filter_odd_squares
    |> build_pixle_map
    |> draw_image
    |> save_image(input)
  end

  @doc """
    Saves and image as a png file to main program folder.
  """

  def save_image(image, input) do
    File.write("#{input}.png", image)
  end

  @doc """
    Returns an image
  """

  def draw_image(%Identicon.Image{color: color, pixle_map: pixle_map}) do
    image = :egd.create(250,250)
    fill = :egd.color(color)

    Enum.each pixle_map, fn({start,stop}) ->
      :egd.filledRectangle(image, start, stop, fill)
    end

    :egd.render(image)
  end

  def build_pixle_map(%Identicon.Image{grid: grid} = image) do
    pixle_map = Enum.map grid, fn({_code, index}) ->
      horizontal = rem(index, 5) * 50
      vertical = div(index, 5) * 50

      top_left = {horizontal, vertical}
      bottom_right = {horizontal + 50, vertical + 50}

      {top_left, bottom_right}
    end

    %Identicon.Image{image | pixle_map: pixle_map}
  end

  def filter_odd_squares(%Identicon.Image{grid: grid} = image) do
    grid = Enum.filter grid, fn({code, _index})->
      rem(code, 2) == 0
    end

    %Identicon.Image{image | grid: grid}
  end

  def build_grid(%Identicon.Image{hex: hex} = image) do
    grid =
      hex
      |> Enum.chunk(3)
      |> Enum.map(&mirror_row/1)
      |> List.flatten
      |> Enum.with_index

    %Identicon.Image{image | grid: grid}
  end

  def mirror_row(row) do
    [first, second | _tail] = row
    row ++ [second, first]
  end

  @doc """
    Returns a color to be used for ideticon using the first three values of hex code
  """

  def pick_color(image) do
    %Identicon.Image{hex: [r,g,b | _tail]} = image
    %Identicon.Image{image | color: {r,g,b}}
  end

  @doc """
    Returns a list of hex values using the crypto :md5 library based on input
  """

  def hash_input(input) do
    hex = :crypto.hash(:md5, input)
    |> :binary.bin_to_list

    %Identicon.Image{hex: hex}
  end
end

# &mirror_row/1 --> to pass a reference to function

#  ++ Joins list together

#pattern match to get access to first 3 properties
# [r,g,b | _tail] = hex_list      since we arent using hex_list --> we cadirectly patter match

# def pick_color(%Identicon.Image{hex: [r,g,b | _tail]} = image) do
#   %Identicon.Image{image | color: {r,g,b}}
# end
# we can directy patter match in teh fucntion input
